/* 
 Guarded Commands example 11 -- n queens problem * 1996-12-20, 2009-10-01

 Running this with the micro-C interpreter "eval" written in F#:
 Finding the 724 solutions to the 10 queens problem takes 6-7
 seconds on a 1.6 GHz Pentium M.

 Running this micro-C program, when compiled to bytecode: 
 Finding the 724 solutions to the 10 queens problem takes 0.6
 seconds with an abstract machine implemented in Java and executed
 on Sun JDK 1.5 Hotspot on a 1.6 GHz Pentium M running Windows XP.  
 It takes 0.2 seconds with an abstract machine implemented in C
 and executed on a 1.86 GHz Intel Xeon running Linux.
*/

begin
  n: int,
  i: int,
  u: int,
  sol: int,
  used: bool[100],
  diag1: bool[100],
  diag2: bool[100],
  col: int[100];

  n := 10;
  sol := 0;
  u := 1;
  do u <= n -> {
    used[u] := false;
    u := u + 1}
  od;

  u := 1;
  do u <= 2*n -> {
    diag1[u] := false;
    diag2[u] := false;
    u := u + 1}
  od;

  i := 1;
  u := 1;
  do i > 0 -> {
      do i <= n && i <> 0 -> {
          do u <= n && (used[u] || diag1[u-i+n] || diag2[u+i]) -> u:=u+1 od;
          if u <= n -> { /* not used[u]; fill col[i] then try col[i+1] */
              col[i] := u;
              used[u] := true;
              diag1[u-i+n] := true;
              diag2[u+i] := true;
              i := i+1;
              u := 1
            }
          | true    -> { /* backtrack; try to find a new col[i-1] */
              i := i - 1;
              if i > 0 -> {
                u := col[i];
                used[u] := false;
                diag1[u-i+n] := false;
                diag2[u+i] := false;
                u := u + 1}
              | true -> skip
              fi
            }
          fi
        }
      od;

      if i > n -> {  /* output solution, then backtrack */
        sol := sol + 1;
        i := i-1;
        if i > 0 -> {
          u := col[i];
          used[u] := false;
          diag1[u-i+n] := false;
          diag2[u+i] := false;
          u := u + 1}
        | true -> skip
        fi}
      | true -> skip
      fi
  } od;

  print sol
end
