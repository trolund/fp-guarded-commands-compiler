begin
  n: int,
  i: int,
  u: int,
  sol: int,
  used: bool[100],
  diag1: bool[100],
  diag2: bool[100],
  col: int[100];

  n := 10;
  sol := 0;
  u := 1;
  do u <= n -> {
    used[u] := false;
    u := u + 1}
  od;

  u := 1;
  do u <= 2*n -> {
    diag1[u], diag2[u] := false,false;
    u := u + 1}
  od;

  i,u := 1,1;
  do i > 0 -> {
      do i <= n && i <> 0 -> {
          do u <= n && (used[u] || diag1[u-i+n] || diag2[u+i]) -> u:=u+1 od;
          if u <= n -> {
              col[i] := u;
              used[u],diag1[u-i+n],diag2[u+i] := true, true, true;
              i,u := i+1,1
            }
          | true    -> {
              i := i - 1;
              if i > 0 -> {
                u := col[i];
                used[u],diag1[u-i+n],diag2[u+i] := false, false, false;
                u := u + 1}
              | true -> skip
              fi
            }
          fi
        }
      od;

      if i > n -> {
        sol := sol + 1;
        i := i-1;
        if i > 0 -> {
          u := col[i];
          used[u],diag1[u-i+n],diag2[u+i] := false, false, false;
          u := u + 1}
        | true -> skip
        fi}
      | true -> skip
      fi
  } od;

  print sol
end
